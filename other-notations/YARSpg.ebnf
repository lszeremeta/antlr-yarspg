/*
 YARS-PG grammar in Extended Backus-Naur Form (EBNF) notation
 Based on YARS-PG grammar in ANTLR4
 
 See more at:
 https://github.com/lszeremeta/antlr-yarspg
 
 [The "BSD licence"]
 Copyright (c) 2018-2019, ≈Åukasz Szeremeta (@ University of Bialystok, http://www.uwb.edu.pl/)
 Copyright (c) 2018-2019, Dominik Tomaszuk (@ University of Bialystok, http://www.uwb.edu.pl/)
 All rights reserved.

 Based on YARS grammar
 (https://github.com/lszeremeta/antlr-yars/blob/master/yars/YARS.g4)
 distributed under BSD licence.

 Redistribution and use in source and binary forms, with or without
 modification, are permitted provided that the following conditions
 are met:
 1. Redistributions of source code must retain the above copyright
    notice, this list of conditions and the following disclaimer.
 2. Redistributions in binary form must reproduce the above copyright
    notice, this list of conditions and the following disclaimer in the
    documentation and/or other materials provided with the distribution.
 3. The name of the author may not be used to endorse or promote products
    derived from this software without specific prior written permission.

 THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
 IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
*/

yarspg   ::= statement+
statement
         ::= node
           | relationship
           | prefixDirective
           | metadata
prefixDirective
         ::= pname CONTEXT
iri      ::= CONTEXT
pname    ::= ':' ALNUM_PLUS ':'
pn_local ::= ALNUM_PLUS
metadata ::= '@' ( pn_local pname STRING | iri ':' ( STRING | iri ) )
annotation ::= ( pn_local pname STRING | iri ':' ( STRING | iri ) )
node
         ::= '<' node_id '>' ( '{' node_label ( ',' node_label )+ '}' )? ( '[' node_prop ( ',' node_prop )+ ']' )? ( '?' annotation ( ',' annotation )+ )?
relationship
         ::= directed
           | undirected
directed ::= '(' node_id ')' '-' ('<' rel_id '>')? '{' rel_label '}' ( '[' rel_prop ( ',' rel_prop )+ ']' )? '->' '(' node_id ')' ( '?' annotation ( ',' annotation )+ )?
undirected
         ::= '(' node_id ')' '-' ('<' rel_id '>')? '{' rel_label '}' ( '[' rel_prop ( ',' rel_prop )+ ']' )? '-' '(' node_id ')' ( '?' annotation ( ',' annotation )+ )?
node_id  ::= STRING
node_label
         ::= STRING
node_prop
         ::= key ':' value         
rel_id   ::= STRING
rel_label
         ::= STRING
rel_prop
         ::= key ':' value 
key      ::= STRING
value    ::= primitive_datatype
           | complex_datatype
primitive_datatype
         ::= STRING
           | NUMBER
           | 'null'
           | BOOL
complex_datatype
         ::= list
list     ::= '[' value ( ',' value )* ']'
_        ::= COMMENT
           | WS
          /* ws: definition */

<?TOKENS?>

COMMENT  ::= '#' [^#xd#xa#xc]*
STRING   ::= STRING_LITERAL_QUOTE
NUMBER   ::= [0-9]+ '.'? [0-9]*
BOOL     ::= 'true'
           | 'false'
STRING_LITERAL_QUOTE
         ::= '"' ( [^"\#xd#xa] | "'" | '\"' )* '"'
ALNUM_PLUS
         ::= PN_CHARS_BASE ( ( PN_CHARS | '.' )* PN_CHARS )?
CONTEXT  ::= '<' ( PN_CHARS | '.' | ':' | '/' | '\' | '#' | '@' | '%' | '&' | UCHAR )* '>'
PN_CHARS ::= PN_CHARS_U
           | [-0-9#xB7#x0300-#x036F#x203F-#x2040]
PN_CHARS_U
         ::= PN_CHARS_BASE
           | '_'
UCHAR    ::= ( '\u' | '\U' HEX HEX HEX HEX ) HEX HEX HEX HEX
PN_CHARS_BASE
         ::= [A-Za-z0-9#xC0-#xD6#xD8-#xF6#xF8-#x2FF#x370-#x37D#x37F-#x1FFF#x200C-#x200D#x2070-#x218F#x2C00-#x2FEF#x3001-#xD7FF#xF900-#xFDCF#xFDF0-#xFFFD]
HEX      ::= [0-9A-Fa-f]
WS       ::= [ #x9#xa#xd]+